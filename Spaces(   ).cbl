000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.    MXBPW026.
000030 AUTHOR.        TEST.
000040 INSTALLATION.  TEST.
000050 DATE-WRITTEN.  MAY 1999.
000060
000070*================================================================*
000080*                                                                |
000090* MAX SYSTEM STANDARD PROGRAM DESCRIPTION AREA                   |
000100*                                                                |
000110*================================================================*
000120* PROGRAM  | MXBPW026 | TRANS ID | N/A  |                        |
000130*----------+----------+----------+------+------------------------*
000140* DESCRIPTION:                                                   |
000150*                                                                |
000160*      LOAD XEROX FORM TABLE                                     |
000170*             COMMON BATCH SUBROUTINE                            |
000180*                                                                |
000190* THIS PROGRAM WILL RETURN THE XEROX FORM ID, XEROX LOGO ID AND  |
000200* NUMBER OF COPIES OF THE REPORT TO BE PRINTED TO THE CALLED PGM |
000210*                                                                |
000220*----------------------------------------------------------------*
000230*                                                                |
000240*================================================================*
000250*   CALL   | PROGRAM DESCRIPTION                                 |
000260*----------+-----------------------------------------------------*
000270* MXBCABT  | MAX STANDARD ABORT ROUTINE.                         |
000280*================================================================*
000290*   PLAN   | BIND INCLUDE MEMBERS                                |
000300*================================================================*
000310*  TABLE   |   VIEW   |     DESCRIPTION                          |
000320*----------+-----------------------------------------------------*
000330* TBMCNFM  | VWMCNFM  | CONTROL ENTITY FORM TYPES                |
000340*----------+-----------------------------------------------------*
000350* TBMCN00  | VWMCN00  | CONTROL ENTITY                           |
000510
000520 ENVIRONMENT DIVISION.
000530
000540 CONFIGURATION SECTION.
000550 SOURCE-COMPUTER. IBM-3090.
000560 OBJECT-COMPUTER. IBM-3090.
000570
000580 DATA DIVISION.
000590
000600 WORKING-STORAGE SECTION.
000610
000620 01  WS-MISC-FIELDS.
000630     05  WS-COMPANY-HOLD-FIELD         PIC 9(04) VALUE ZERO.
000640     05  WS-FIRST-TIME-FLAG            PIC X(01) VALUE ' '.
000650         88  FIRST-TIME                          VALUE ' '.
000660         88  NOT-FIRST-TIME                      VALUE 'N'.
000670     05  WS-XEROX-FORM-FLAG            PIC X(01) VALUE 'N'.
000680         88  WS-MORE-XEROX-ENTRY                 VALUE 'N'.
000690         88  WS-NO-MORE-XEROX-ENTRY              VALUE 'Y'.
000700     05  WS-XEROX-KEY-FLAG             PIC X(01) VALUE 'N'.
000710         88  WS-NO-MORE-XEROX-KEY                VALUE 'Y'.
000720     05  WS-XEROX-CODE-FLAG            PIC X(01) VALUE 'N'.
000730         88  WS-SEARCH-FOR-FORM                  VALUE 'S'.
000740         88  WS-XEROX-CODE-FOUND                 VALUE 'Y'.
000750         88  WS-XEROX-CODE-NOT-FOUND             VALUE 'N'.
000760     05  WS-PGM-SPEC-FLAG              PIC X(01) VALUE 'N'.
000770         88  WS-SEARCH-FOR-PGM-SPEC              VALUE 'S'.
000780         88  WS-PGM-SPEC-FOUND                   VALUE 'Y'.
000790         88  WS-PGM-SPEC-NOT-FOUND               VALUE 'N'.
000800     05  WS-SPEC-FLAG                  PIC X(01) VALUE 'N'.
000810         88  WS-SEARCH-FOR-SPEC                  VALUE 'S'.
000820         88  WS-SPEC-FOUND                       VALUE 'Y'.
000830         88  WS-SPEC-NOT-FOUND                   VALUE 'N'.
000840     05  WS-CMPY-FLAG                  PIC X(01) VALUE 'N'.
000850         88  WS-SEARCH-FOR-CMPY-SPEC             VALUE 'S'.
000860         88  WS-CMPY-SPEC-FOUND                  VALUE 'Y'.
000870         88  WS-CMPY-SPEC-NOT-FOUND              VALUE 'N'.
000880     05  WS-PGM-SPEC-HNDL-IND          PIC X(01) VALUE 'N'.
000890         88  WS-MORE-PGM-SPEC-HNDL-ENTRY         VALUE 'N'.
000900         88  WS-NO-MORE-PGM-SPEC-HNDL-ENTRY      VALUE 'Y'.
000910     05  WS-SPEC-HNDL-IND              PIC X(01) VALUE 'N'.
000920         88  WS-MORE-SPEC-HNDL-ENTRY             VALUE 'N'.
000930         88  WS-NO-MORE-SPEC-HNDL-ENTRY          VALUE 'Y'.
000940     05  WS-CMPY-SPEC-IND              PIC X(01) VALUE 'N'.
000950         88  WS-MORE-CMPY-SPEC-ENTRY             VALUE 'N'.
000960         88  WS-NO-MORE-CMPY-SPEC-ENTRY          VALUE 'Y'.
000970     05  WS-SAVE-CUST-CNTL-DD          PIC S9(04) COMP VALUE +0.
000980     05  WS-PGM-ABEND-CD               PIC S9(4) VALUE +3600 COMP.
000990     05  WS-CNTL-ENT-NO                PIC S9(04) COMP.
000991     05  WS-COMPANY                    PIC S9(04) COMP.
000992     05  WS-BRANCH                     PIC S9(04) COMP.
001000     05  WS-SUB                        PIC S9(04) COMP.
001010
001020******************************************************************
001030*           X E R O X   F O R M   C O D E S   T A B L E          *
001040*                                                                *
001050*  THIS TABLE CONTAINS THE XEROX FORM CODES BY CONTROL ENTITY.   *
001060******************************************************************
001070
001080 01  WS-XEROX-FORM-CODE-TABLE.
001090     05  WS-XEROX-TABLE OCCURS 050 TIMES
001100         INDEXED BY XERX-SUB.
001110         10  WS-XEROX-FORM-KEY.
001120             15  WS-XEROX-PGM-NAME     PIC X(08) VALUE SPACES.
001130             15  WS-XEROX-XTRA-QLF     PIC X(08) VALUE SPACES.
001140         10  WS-XEROX-FORM-ROWS OCCURS 500 TIMES
001150             INDEXED BY XERX-SUB1.
001160             15  WS-XEROX-DATA-KEY.
001170                 20  WS-XEROX-CNTL-ENT-NO  PIC S9(04) COMP.
001180                 20  WS-XEROX-LANG-CODE    PIC X(06) VALUE SPACES.
001190                 20  WS-XEROX-CNTRY-CODE   PIC X(03) VALUE SPACES.
001200             15  WS-XEROX-FORM-ID      PIC X(08) VALUE SPACES.
001210             15  WS-XEROX-LOGO-ID      PIC X(08) VALUE SPACES.
001220             15  WS-XEROX-COPY-CNT     PIC S9(04) COMP VALUE +0.
001230
001240 01  WS-PGM-SPEC-HNDL-TABLE.
001250     05  WS-PGM-TABLE OCCURS 5000 TIMES
001260         INDEXED BY PGM-SUB1.
001270         10  WS-PGM-SPEC-HNDL-KEY.
001280             15  WS-PGM-COMPANY-NO     PIC S9(9) USAGE COMP.
001290             15  WS-PGM-COUNTRY-CODE   PIC X(3).
001300             15  WS-PGM-CUST-NO        PIC S9(9) USAGE COMP.
001310             15  WS-PGM-LOC-NO         PIC S9(4) USAGE COMP.
001320         10  WS-PGM-SPEC-HNDL-FLAG     PIC X(2).
001330
001340 01  WS-SPEC-HNDL-TABLE.
001350     05  WS-TABLE OCCURS 500 TIMES
001360         INDEXED BY SUB1.
001370         10  WS-SPEC-HNDL-KEY.
001380             15  WS-COMPANY-NO         PIC S9(9) USAGE COMP.
001390             15  WS-COUNTRY-CODE       PIC X(3).
001400             15  WS-CUST-NO            PIC S9(9) USAGE COMP.
001410             15  WS-LOC-NO             PIC S9(4) USAGE COMP.
001420         10  WS-SPEC-HNDL-FLAG         PIC X(2).
001430
001440 01  WS-COMPANY-TABLE.
001450     05  WS-CMPY-TABLE OCCURS 100 TIMES
001460         INDEXED BY CMPY-SUB1.
001470         10  WS-CMPY-KEY.
001480             15  WS-CMPY-NO            PIC S9(9) USAGE COMP.
001490         10  WS-CMPY-SPEC-HNDL-FLAG    PIC X(2).
001500
001510 01  WS-SEARCH-XEROX-KEY.
001520     05  WS-SXK-RPT-PGM-ID             PIC X(08) VALUE SPACES.
001530     05  WS-SXK-RPT-PGM-QLF-ID         PIC X(08) VALUE SPACES.
001540
001550 01  WS-SEARCH-XEROX-DATA-KEY.
001560     05  WS-SXK-CNTL-ENT-NO            PIC S9(04) COMP VALUE +0.
001570     05  WS-SXK-LANG-CODE              PIC X(06) VALUE SPACES.
001580     05  WS-SXK-CNTRY-CODE             PIC X(03) VALUE SPACES.
001590
001600 01  WS-SEARCH-PGM-SPEC-KEY.
001610     05  WS-SPK-COMPANY-NO             PIC S9(9) USAGE COMP.
001620     05  WS-SPK-COUNTRY-CODE           PIC X(3).
001630     05  WS-SPK-CUST-NO                PIC S9(9) USAGE COMP.
001640     05  WS-SPK-LOC-NO                 PIC S9(4) USAGE COMP.
001650
001660 01  WS-SEARCH-SPEC-KEY.
001670     05  WS-SSK-COMPANY-NO             PIC S9(9) USAGE COMP.
001680     05  WS-SSK-COUNTRY-CODE           PIC X(3).
001690     05  WS-SSK-CUST-NO                PIC S9(9) USAGE COMP.
001700     05  WS-SSK-LOC-NO                 PIC S9(4) USAGE COMP.
001710
001720 01  WS-SEARCH-CMPY-KEY.
001730     05  WS-SCK-COMPANY-NO             PIC S9(9) USAGE COMP.
001740
001750******************************************************************
001760*  CONTROL ENTITY                                                *
001770******************************************************************
001780
001790     EXEC SQL
001800         INCLUDE VWMCN00
001810     END-EXEC.
001820
001830******************************************************************
001840*  CONTROL ENTITY FORM TYPES                                     *
001850******************************************************************
001860
001870     EXEC SQL
001880         INCLUDE VWMCNFM
001890     END-EXEC.
001900
001910******************************************************************
001920*  CONTROL ENTITY SPECIAL HANDLING FLAG                          *
001930******************************************************************
001940
001950     EXEC SQL
001960         INCLUDE VWMCTSHD
001970     END-EXEC.
001980
001990******************************************************************
002000*  ABNORMAL TERMINATION AREA.                                    *
002010******************************************************************
002020
002030     EXEC SQL
002040         INCLUDE MXWW03
002050     END-EXEC.
002060
002070     EXEC SQL
002080         INCLUDE SQLCA
002090     END-EXEC.
002100
002110******************************************************************
002120*  CURSOR CONTAINING THE XEROX FORM DATA.                        *
002130******************************************************************
002140
002150     EXEC SQL
002160         DECLARE VWMCNFM-01 CURSOR FOR
002170         SELECT CNTL_ENT_NO,
002180                RPT_PGM_ID,
002190                RPT_PGM_QLF_ID,
002200                XEROX_FORM_ID,
002210                XEROX_LOGO_ID,
002220                LANGUAGE_CODE,
002230                COUNTRY_CODE,
002240                FORM_COPY_CNT
002250         FROM VWMCNFM
002260         WHERE RPT_PGM_ID     = :DCLVWMCNFM.RPT-PGM-ID
002270           AND RPT_PGM_QLF_ID = :DCLVWMCNFM.RPT-PGM-QLF-ID
002280         ORDER BY CNTL_ENT_NO,
002290                  RPT_PGM_ID,
002300                  RPT_PGM_QLF_ID,
002310                  LANGUAGE_CODE,
002320                  COUNTRY_CODE
002330     END-EXEC.
002340
002350******************************************************************
002360*  CURSOR CONTAINING THE PGM SPECIFIC SPECIAL HANDLING           *
002370******************************************************************
002380
002390     EXEC SQL
002400         DECLARE VWMCTSHD-01 CURSOR FOR
002410         SELECT COMPANY_NO
002420               ,COUNTRY_CODE
002430               ,CUST_NO
002440               ,LOC_NO
002450               ,SPEC_HNDL_FLAG
002460               ,EXP_DATE
002470         FROM VWMCTSHD A
002480         WHERE PGM_ID               = :DCLVWMCTSHD.PGM-ID
002490         AND   EXP_DATE             = (SELECT MIN(EXP_DATE)
002500               FROM VWMCTSHD B
002510               WHERE B.EXP_DATE    >= CURRENT DATE
002520               AND   B.PGM_ID       = A.PGM_ID
002530               AND   B.COMPANY_NO   = A.COMPANY_NO
002540               AND   B.COUNTRY_CODE = A.COUNTRY_CODE
002550               AND   B.CUST_NO      = A.CUST_NO
002560               AND   B.LOC_NO       = A.LOC_NO)
002570         ORDER BY  COMPANY_NO
002580                  ,COUNTRY_CODE
002590                  ,CUST_NO
002600                  ,LOC_NO
002610     END-EXEC.
002620
002630******************************************************************
002640*  CURSOR CONTAINING THE DEFAULT SPECIAL HANDLING                *
002650******************************************************************
002660
002670     EXEC SQL
002680         DECLARE VWMCTSHD-02 CURSOR FOR
002690         SELECT COMPANY_NO
002700               ,COUNTRY_CODE
002710               ,CUST_NO
002720               ,LOC_NO
002730               ,SPEC_HNDL_FLAG
002740               ,EXP_DATE
002750         FROM VWMCTSHD A
002760         WHERE PGM_ID               = 'XXXXXXXX'
002770         AND   EXP_DATE             = (SELECT MIN(EXP_DATE)
002780               FROM VWMCTSHD B
002790               WHERE B.EXP_DATE    >= CURRENT DATE
002800               AND   B.PGM_ID       = A.PGM_ID
002810               AND   B.COMPANY_NO   = A.COMPANY_NO
002820               AND   B.COUNTRY_CODE = A.COUNTRY_CODE
002830               AND   B.CUST_NO      = A.CUST_NO
002840               AND   B.LOC_NO       = A.LOC_NO)
002850         ORDER BY  COMPANY_NO
002860                  ,COUNTRY_CODE
002870                  ,CUST_NO
002880                  ,LOC_NO
002890     END-EXEC.
002900
002910     EXEC SQL
002920         DECLARE VWMCTSHD-03 CURSOR FOR
002930         SELECT COMPANY_NO
002940               ,SPEC_HNDL_FLAG
002950               ,EXP_DATE
002960         FROM VWMCTSHD A
002970         WHERE PGM_ID               = 'XXXXXXXX'
002980         AND   COUNTRY_CODE         = '   '
002990         AND   CUST_NO              = 0
003000         AND   LOC_NO               = 0
003010         AND   EXP_DATE             = (SELECT MIN(EXP_DATE)
003020               FROM VWMCTSHD B
003030               WHERE B.EXP_DATE    >= CURRENT DATE
003040               AND   B.PGM_ID       = A.PGM_ID
003050               AND   B.COMPANY_NO   = A.COMPANY_NO
003060               AND   B.COUNTRY_CODE = A.COUNTRY_CODE
003070               AND   B.CUST_NO      = A.CUST_NO
003080               AND   B.LOC_NO       = A.LOC_NO)
003090         ORDER BY  COMPANY_NO
003100     END-EXEC.
003110
003120 LINKAGE SECTION.
003130
003140     COPY MXWW26.
003150
003160 PROCEDURE DIVISION USING MXWW26-WORK-AREA.
003170
003180 0000-MAINLINE.
003190
003200     PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.
003210     PERFORM 2000-PROCESS-LINKAGE     THRU 2000-EXIT.
003220     PERFORM 2200-GET-XEROX           THRU 2200-EXIT.
003230     PERFORM 2400-RTN-SPEC-HNDL       THRU 2400-EXIT.
003240     PERFORM 3000-TERMINATION         THRU 3000-EXIT.
003250
003260     GOBACK.
003270
003280 0000-EXIT.  EXIT.
003290
003300 1000-INITIALIZATION.
003310
003320     IF FIRST-TIME
003330        MOVE SPACES              TO WS-XEROX-FORM-CODE-TABLE
003340                                    WS-PGM-SPEC-HNDL-TABLE
003350                                    WS-SPEC-HNDL-TABLE
003360                                    WS-COMPANY-TABLE
003370        PERFORM 1500-STORE-SPEC-HNDL THRU 1500-EXIT
003380        SET NOT-FIRST-TIME       TO TRUE
003390     END-IF.
003400
003410     MOVE MXWW26-RPT-PGM-ID      TO RPT-PGM-ID OF DCLVWMCNFM.
003420     MOVE MXWW26-RPT-PGM-QLF-ID  TO RPT-PGM-QLF-ID OF DCLVWMCNFM.
003430
003440     EXEC SQL
003450         OPEN VWMCNFM-01
003460     END-EXEC.
003470
003480     IF SQLCODE NOT = 0
003490         SET ABT-ERROR-IS-DB2 TO TRUE
003500         SET ABT-DO-ABEND     TO TRUE
003510         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
003520         MOVE 'OPEN    '      TO ABT-DA-FUNCTION
003530         MOVE 'VWMCNFM '      TO ABT-DA-ACCESS-NAME
003540         MOVE '1000-01 '      TO ABT-ERROR-SECTION
003550         PERFORM Z-980-ABNORMAL-TERM
003560     END-IF.
003570
003580 1000-EXIT.  EXIT.
003590******************************************************************
003600*  BUILD 3 INTERNAL TABLES CONTAINING SPECIAL HANDLING FOR       *
003610*  1)  PROGRAM SPECIFIC                                          *
003620*  2)  DEFAULT                                                   *
003630*  3)  COMPANIES                                                 *
003640******************************************************************
003650 1500-STORE-SPEC-HNDL.
003660
003670     DISPLAY ' '
003680     DISPLAY '*********************************************'
003690     DISPLAY '* MXBPW026 CALLED FOR THE FIRST TIME        *'
003700     DISPLAY '* CONTROL ENTITY ' MXWW26-CNTL-ENT-NO
003710     DISPLAY '* PROGRAM ID     ' MXWW26-RPT-PGM-ID
003720     DISPLAY '* QUALIFIER      ' MXWW26-RPT-PGM-QLF-ID
003730     DISPLAY '* LANGUAGE       ' MXWW26-LANG-CODE
003740     DISPLAY '* COUNTRY        ' MXWW26-CNTRY-CODE
003750     DISPLAY '* CUSTOMER       ' MXWW26-CUST-NO
003760     DISPLAY '* LOCATION       ' MXWW26-LOC-NO
003770     DISPLAY '*********************************************'
003780
003790     MOVE MXWW26-RPT-PGM-ID      TO PGM-ID OF DCLVWMCTSHD.
003800
003810     EXEC SQL
003820         OPEN VWMCTSHD-01
003830     END-EXEC.
003840
003850     IF SQLCODE NOT = 0
003860         SET ABT-ERROR-IS-DB2 TO TRUE
003870         SET ABT-DO-ABEND     TO TRUE
003880         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
003890         MOVE 'OPEN    '      TO ABT-DA-FUNCTION
003900         MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
003910         MOVE '1000-02 '      TO ABT-ERROR-SECTION
003920         PERFORM Z-980-ABNORMAL-TERM
003930     END-IF.
003940
003950     EXEC SQL
003960         OPEN VWMCTSHD-02
003970     END-EXEC.
003980
003990     IF SQLCODE NOT = 0
004000         SET ABT-ERROR-IS-DB2 TO TRUE
004010         SET ABT-DO-ABEND     TO TRUE
004020         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
004030         MOVE 'OPEN    '      TO ABT-DA-FUNCTION
004040         MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
004050         MOVE '1000-03 '      TO ABT-ERROR-SECTION
004060         PERFORM Z-980-ABNORMAL-TERM
004070     END-IF.
004080
004090     EXEC SQL
004100         OPEN VWMCTSHD-03
004110     END-EXEC.
004120
004130     IF SQLCODE NOT = 0
004140         SET ABT-ERROR-IS-DB2 TO TRUE
004150         SET ABT-DO-ABEND     TO TRUE
004160         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
004170         MOVE 'OPEN    '      TO ABT-DA-FUNCTION
004180         MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
004190         MOVE '1000-04 '      TO ABT-ERROR-SECTION
004200         PERFORM Z-980-ABNORMAL-TERM
004210     END-IF.
004220
004230     SET  WS-MORE-PGM-SPEC-HNDL-ENTRY TO TRUE.
004240
004250     DISPLAY ' '
004260     DISPLAY '********************************************'
004270     DISPLAY '* W026 - LOADING PROGRAM SPECIFIC SPEC HND *'
004280     DISPLAY '* '
004290
004300     PERFORM 1510-GET-NEXT-PGM-SPEC-HNDL THRU 1510-EXIT
004310     VARYING PGM-SUB1 FROM 1 BY 1
004320     UNTIL   PGM-SUB1 > 5000
004330     OR      WS-NO-MORE-PGM-SPEC-HNDL-ENTRY.
004340
004350     DISPLAY '********************************************'
004360
004370     IF WS-MORE-PGM-SPEC-HNDL-ENTRY
004380         DISPLAY '*******************************************'
004390         DISPLAY 'THE PGM SPEC HNDL TABLE (A WORKING         '
004400         DISPLAY 'STORAGE TABLE) IS FULL.                    '
004410         DISPLAY 'INCREASE THE TABLE SIZE.                   '
004420         DISPLAY '*******************************************'
004430         MOVE 'USER' TO ABT-ERROR-ACTIVITY
004440         SET  ABT-DO-ABEND TO TRUE
004450         MOVE WS-PGM-ABEND-CD TO ABT-ERROR-ABEND-CODE
004460         MOVE 'LOAD TBL' TO ABT-DA-FUNCTION
004470         MOVE '        ' TO ABT-DA-ACCESS-NAME
004480         MOVE '1500-01 ' TO ABT-ERROR-SECTION
004490         PERFORM Z-980-ABNORMAL-TERM
004500     END-IF.
004510
004520     DISPLAY ' '
004530     DISPLAY '********************************************'
004540     DISPLAY '* W026 - LOADING GLOBAL SPECIAL HANDLING   *'
004550     DISPLAY '* '
004560
004570     PERFORM 1520-GET-NEXT-SPEC-HNDL THRU 1520-EXIT
004580         VARYING SUB1 FROM 1 BY 1
004590         UNTIL SUB1 > 500 OR
004600               WS-NO-MORE-SPEC-HNDL-ENTRY.
004610
004620     DISPLAY '********************************************'
004630
004640     IF WS-MORE-SPEC-HNDL-ENTRY
004650         DISPLAY '*******************************************'
004660         DISPLAY 'THE DEFAULT SPEC HNDL TABLE (A WORKING     '
004670         DISPLAY 'STORAGE TABLE) IS FULL.                    '
004680         DISPLAY 'INCREASE THE TABLE SIZE.                   '
004690         DISPLAY '*******************************************'
004700         MOVE 'USER' TO ABT-ERROR-ACTIVITY
004710         SET  ABT-DO-ABEND TO TRUE
004720         MOVE WS-PGM-ABEND-CD TO ABT-ERROR-ABEND-CODE
004730         MOVE 'LOAD TBL' TO ABT-DA-FUNCTION
004740         MOVE '        ' TO ABT-DA-ACCESS-NAME
004750         MOVE '1500-02 ' TO ABT-ERROR-SECTION
004760         PERFORM Z-980-ABNORMAL-TERM
004770     END-IF.
004780
004790     PERFORM 1530-GET-NEXT-CMPY-SPEC-HNDL THRU 1530-EXIT
004800         VARYING CMPY-SUB1 FROM 1 BY 1
004810         UNTIL CMPY-SUB1 > 100 OR
004820               WS-NO-MORE-CMPY-SPEC-ENTRY.
004830
004840     IF WS-MORE-CMPY-SPEC-ENTRY
004850         DISPLAY '*******************************************'
004860         DISPLAY 'THE DEFAULT SPEC HNDL TABLE (A WORKING     '
004870         DISPLAY 'STORAGE TABLE) IS FULL.                    '
004880         DISPLAY 'INCREASE THE TABLE SIZE.                   '
004890         DISPLAY '*******************************************'
004900         MOVE 'USER' TO ABT-ERROR-ACTIVITY
004910         SET  ABT-DO-ABEND TO TRUE
004920         MOVE WS-PGM-ABEND-CD TO ABT-ERROR-ABEND-CODE
004930         MOVE 'LOAD TBL' TO ABT-DA-FUNCTION
004940         MOVE '        ' TO ABT-DA-ACCESS-NAME
004950         MOVE '1500-03 ' TO ABT-ERROR-SECTION
004960         PERFORM Z-980-ABNORMAL-TERM
004970     END-IF.
004980
004990     EXEC SQL
005000         CLOSE VWMCTSHD-01
005010     END-EXEC.
005020
005030     IF SQLCODE NOT = 0
005040         SET ABT-ERROR-IS-DB2 TO TRUE
005050         SET ABT-DO-ABEND     TO TRUE
005060         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
005070         MOVE 'CLOSE   '      TO ABT-DA-FUNCTION
005080         MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
005090         MOVE '3000-02 '      TO ABT-ERROR-SECTION
005100         PERFORM Z-980-ABNORMAL-TERM
005110     END-IF.
005120
005130     EXEC SQL
005140         CLOSE VWMCTSHD-02
005150     END-EXEC.
005160
005170     IF SQLCODE NOT = 0
005180         SET ABT-ERROR-IS-DB2 TO TRUE
005190         SET ABT-DO-ABEND     TO TRUE
005200         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
005210         MOVE 'CLOSE   '      TO ABT-DA-FUNCTION
005220         MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
005230         MOVE '3000-03 '      TO ABT-ERROR-SECTION
005240         PERFORM Z-980-ABNORMAL-TERM
005250     END-IF.
005260
005270     EXEC SQL
005280         CLOSE VWMCTSHD-03
005290     END-EXEC.
005300
005310     IF SQLCODE NOT = 0
005320         SET ABT-ERROR-IS-DB2 TO TRUE
005330         SET ABT-DO-ABEND     TO TRUE
005340         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
005350         MOVE 'CLOSE   '      TO ABT-DA-FUNCTION
005360         MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
005370         MOVE '3000-04 '      TO ABT-ERROR-SECTION
005380         PERFORM Z-980-ABNORMAL-TERM
005390     END-IF.
005400
005410 1500-EXIT.  EXIT.
005420******************************************************************
005430*  PLACE NEXT PGM SPEC HNDL CODE IN THE TABLE.                   *
005440******************************************************************
005450 1510-GET-NEXT-PGM-SPEC-HNDL.
005460
005470     EXEC SQL
005480         FETCH VWMCTSHD-01
005490          INTO :DCLVWMCTSHD.COMPANY-NO
005500              ,:DCLVWMCTSHD.COUNTRY-CODE
005510              ,:DCLVWMCTSHD.CUST-NO
005520              ,:DCLVWMCTSHD.LOC-NO
005530              ,:DCLVWMCTSHD.SPEC-HNDL-FLAG
005540              ,:DCLVWMCTSHD.EXP-DATE
005550     END-EXEC.
005560
005570     EVALUATE SQLCODE
005580     WHEN 0
005590          DISPLAY '* ' COMPANY-NO     OF DCLVWMCTSHD ' '
005600                       COUNTRY-CODE   OF DCLVWMCTSHD ' '
005610                       CUST-NO        OF DCLVWMCTSHD ' '
005620                       LOC-NO         OF DCLVWMCTSHD ' '
005630                       SPEC-HNDL-FLAG OF DCLVWMCTSHD
005640          MOVE COMPANY-NO OF DCLVWMCTSHD TO
005650               WS-PGM-COMPANY-NO     (PGM-SUB1)
005660          MOVE COUNTRY-CODE OF DCLVWMCTSHD TO
005670               WS-PGM-COUNTRY-CODE   (PGM-SUB1)
005680          MOVE CUST-NO OF DCLVWMCTSHD TO
005690               WS-PGM-CUST-NO        (PGM-SUB1)
005700          MOVE LOC-NO OF DCLVWMCTSHD TO
005710               WS-PGM-LOC-NO         (PGM-SUB1)
005720          MOVE SPEC-HNDL-FLAG OF DCLVWMCTSHD TO
005730               WS-PGM-SPEC-HNDL-FLAG (PGM-SUB1)
005740
005750         WHEN +100
005760             SET WS-NO-MORE-PGM-SPEC-HNDL-ENTRY TO TRUE
005770
005780         WHEN OTHER
005790             SET ABT-ERROR-IS-DB2 TO TRUE
005800             SET ABT-DO-ABEND     TO TRUE
005810             MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
005820             MOVE 'FETCH   '      TO ABT-DA-FUNCTION
005830             MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
005840             MOVE '1510-   '      TO ABT-ERROR-SECTION
005850             PERFORM Z-980-ABNORMAL-TERM
005860     END-EVALUATE.
005870
005880 1510-EXIT.
005890     EXIT.
005900
005910 1520-GET-NEXT-SPEC-HNDL.
005920
005930     EXEC SQL
005940         FETCH VWMCTSHD-02
005950          INTO :DCLVWMCTSHD.COMPANY-NO
005960              ,:DCLVWMCTSHD.COUNTRY-CODE
005970              ,:DCLVWMCTSHD.CUST-NO
005980              ,:DCLVWMCTSHD.LOC-NO
005990              ,:DCLVWMCTSHD.SPEC-HNDL-FLAG
006000              ,:DCLVWMCTSHD.EXP-DATE
006010     END-EXEC.
006020
006030     EVALUATE SQLCODE
006040     WHEN 0
006050          DISPLAY '* ' COMPANY-NO     OF DCLVWMCTSHD ' '
006060                       COUNTRY-CODE   OF DCLVWMCTSHD ' '
006070                       CUST-NO        OF DCLVWMCTSHD ' '
006080                       LOC-NO         OF DCLVWMCTSHD ' '
006090                       SPEC-HNDL-FLAG OF DCLVWMCTSHD
006100          MOVE COMPANY-NO OF DCLVWMCTSHD TO
006110               WS-COMPANY-NO         (SUB1)
006120          MOVE COUNTRY-CODE OF DCLVWMCTSHD TO
006130               WS-COUNTRY-CODE       (SUB1)
006140          MOVE CUST-NO OF DCLVWMCTSHD TO
006150               WS-CUST-NO            (SUB1)
006160          MOVE LOC-NO OF DCLVWMCTSHD TO
006170               WS-LOC-NO             (SUB1)
006180          MOVE SPEC-HNDL-FLAG OF DCLVWMCTSHD TO
006190               WS-SPEC-HNDL-FLAG     (SUB1)
006200
006210         WHEN +100
006220             SET WS-NO-MORE-SPEC-HNDL-ENTRY TO TRUE
006230
006240         WHEN OTHER
006250             SET ABT-ERROR-IS-DB2 TO TRUE
006260             SET ABT-DO-ABEND     TO TRUE
006270             MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
006280             MOVE 'FETCH   '      TO ABT-DA-FUNCTION
006290             MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
006300             MOVE '1520-   '      TO ABT-ERROR-SECTION
006310             PERFORM Z-980-ABNORMAL-TERM
006320     END-EVALUATE.
006330
006340 1520-EXIT.
006350     EXIT.
006360
006370 1530-GET-NEXT-CMPY-SPEC-HNDL.
006380
006390     EXEC SQL
006400         FETCH VWMCTSHD-03
006410          INTO :DCLVWMCTSHD.COMPANY-NO
006420              ,:DCLVWMCTSHD.SPEC-HNDL-FLAG
006430              ,:DCLVWMCTSHD.EXP-DATE
006440     END-EXEC.
006450
006460     EVALUATE SQLCODE
006470         WHEN 0
006480             MOVE COMPANY-NO OF DCLVWMCTSHD TO
006490                  WS-CMPY-NO                  (CMPY-SUB1)
006500             MOVE SPEC-HNDL-FLAG OF DCLVWMCTSHD TO
006510                  WS-CMPY-SPEC-HNDL-FLAG      (CMPY-SUB1)
006520
006530         WHEN +100
006540             SET WS-NO-MORE-CMPY-SPEC-ENTRY TO TRUE
006550
006560         WHEN OTHER
006570             SET ABT-ERROR-IS-DB2 TO TRUE
006580             SET ABT-DO-ABEND     TO TRUE
006590             MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
006600             MOVE 'FETCH   '      TO ABT-DA-FUNCTION
006610             MOVE 'VWMCTSHD '     TO ABT-DA-ACCESS-NAME
006620             MOVE '1520-   '      TO ABT-ERROR-SECTION
006630             PERFORM Z-980-ABNORMAL-TERM
006640     END-EVALUATE.
006650
006660 1530-EXIT.
006670     EXIT.
006680
006690/*****************************************************************
006700*  PROCESS THE CURRENT RECORD                                    *
006710******************************************************************
006720 2000-PROCESS-LINKAGE.
006730
006740     MOVE MXWW26-RPT-PGM-ID      TO WS-SXK-RPT-PGM-ID
006750     MOVE MXWW26-RPT-PGM-QLF-ID  TO WS-SXK-RPT-PGM-QLF-ID
006760
006770     PERFORM
006780       VARYING XERX-SUB
006790       FROM    1 BY 1
006800       UNTIL   XERX-SUB >  50
006810       OR WS-XEROX-FORM-KEY (XERX-SUB) = SPACES
006820       OR WS-XEROX-FORM-KEY (XERX-SUB) = WS-SEARCH-XEROX-KEY
006830     END-PERFORM.
006840
006850     IF  XERX-SUB > 50
006860         DISPLAY '*******************************************'
006870         DISPLAY 'THE XEROX FORM KEY  TABLE (A WORKING       '
006880         DISPLAY 'STORAGE TABLE) IS FULL. (OVER 50 FORMS)    '
006890         DISPLAY 'INCREASE THE TABLE SIZE.                   '
006900         DISPLAY '*******************************************'
006910         MOVE 'USER' TO ABT-ERROR-ACTIVITY
006920         SET  ABT-DO-ABEND TO TRUE
006930         MOVE WS-PGM-ABEND-CD TO ABT-ERROR-ABEND-CODE
006940         MOVE 'LOAD TBL' TO ABT-DA-FUNCTION
006950         MOVE '        ' TO ABT-DA-ACCESS-NAME
006960         MOVE '2000-   ' TO ABT-ERROR-SECTION
006970         PERFORM Z-980-ABNORMAL-TERM
006980     END-IF.
006990
007000     IF WS-XEROX-FORM-KEY (XERX-SUB) = WS-SEARCH-XEROX-KEY
007010        SET WS-XEROX-CODE-FOUND TO TRUE
007020     ELSE
007070        SET WS-XEROX-CODE-NOT-FOUND TO TRUE
007080     END-IF.
007090
007100     IF WS-XEROX-CODE-NOT-FOUND
007110        PERFORM 2100-LOAD-XEROX-FORM-TABLE
007120     END-IF.
007130
007140 2000-EXIT.  EXIT.
007150/*****************************************************************
007160*  FILL XEROX FORM CODE TABLE.                                   *
007170******************************************************************
007180 2100-LOAD-XEROX-FORM-TABLE.
007190
007200     SET WS-MORE-XEROX-ENTRY          TO TRUE.
007210
007220     PERFORM 2110-GET-NEXT-XEROX THRU 2110-EXIT
007230         VARYING XERX-SUB1 FROM 1 BY 1
007240         UNTIL XERX-SUB1 > 500 OR
007250               WS-NO-MORE-XEROX-ENTRY.
007260
007270     IF WS-MORE-XEROX-ENTRY
007280         DISPLAY '*******************************************'
007290         DISPLAY 'THE XEROX FORM CODE TABLE (A WORKING       '
007300         DISPLAY 'STORAGE TABLE) IS FULL.                    '
007310         DISPLAY 'INCREASE THE TABLE SIZE.                   '
007320         DISPLAY '*******************************************'
007330         MOVE 'USER' TO ABT-ERROR-ACTIVITY
007340         SET  ABT-DO-ABEND TO TRUE
007350         MOVE WS-PGM-ABEND-CD TO ABT-ERROR-ABEND-CODE
007360         MOVE 'LOAD TBL' TO ABT-DA-FUNCTION
007370         MOVE '        ' TO ABT-DA-ACCESS-NAME
007380         MOVE '2100-   ' TO ABT-ERROR-SECTION
007390         PERFORM Z-980-ABNORMAL-TERM
007400     END-IF.
007410
007420
007430 2100-EXIT.
007440     EXIT.
007450
007460 2110-GET-NEXT-XEROX.
007470******************************************************************
007480*  PLACE NEXT XEROX FORM CODE IN TABLE.                          *
007490******************************************************************
007500
007510     EXEC SQL
007520         FETCH VWMCNFM-01
007530          INTO :DCLVWMCNFM.CNTL-ENT-NO,
007540               :DCLVWMCNFM.RPT-PGM-ID,
007550               :DCLVWMCNFM.RPT-PGM-QLF-ID,
007560               :DCLVWMCNFM.XEROX-FORM-ID,
007570               :DCLVWMCNFM.XEROX-LOGO-ID,
007580               :DCLVWMCNFM.LANGUAGE-CODE,
007590               :DCLVWMCNFM.COUNTRY-CODE,
007600               :DCLVWMCNFM.FORM-COPY-CNT
007610     END-EXEC.
007620
007630     EVALUATE SQLCODE
007640         WHEN 0
007030             MOVE MXWW26-RPT-PGM-ID           TO
007040                  WS-XEROX-PGM-NAME    (XERX-SUB)
007050             MOVE MXWW26-RPT-PGM-QLF-ID       TO
007060                  WS-XEROX-XTRA-QLF    (XERX-SUB)
007650             MOVE CNTL-ENT-NO OF DCLVWMCNFM TO
007660                  WS-XEROX-CNTL-ENT-NO (XERX-SUB, XERX-SUB1)
007670             MOVE XEROX-FORM-ID OF DCLVWMCNFM TO
007680                  WS-XEROX-FORM-ID     (XERX-SUB, XERX-SUB1)
007690             MOVE XEROX-LOGO-ID OF DCLVWMCNFM TO
007700                  WS-XEROX-LOGO-ID     (XERX-SUB, XERX-SUB1)
007710             MOVE LANGUAGE-CODE OF DCLVWMCNFM TO
007720                  WS-XEROX-LANG-CODE   (XERX-SUB, XERX-SUB1)
007730             MOVE COUNTRY-CODE OF DCLVWMCNFM TO
007740                  WS-XEROX-CNTRY-CODE  (XERX-SUB, XERX-SUB1)
007750             MOVE FORM-COPY-CNT OF DCLVWMCNFM TO
007760                  WS-XEROX-COPY-CNT    (XERX-SUB, XERX-SUB1)
007770
007780         WHEN +100
007790             SET WS-NO-MORE-XEROX-ENTRY TO TRUE
007800
007810         WHEN OTHER
007820             SET ABT-ERROR-IS-DB2 TO TRUE
007830             SET ABT-DO-ABEND     TO TRUE
007840             MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
007850             MOVE 'FETCH   '      TO ABT-DA-FUNCTION
007860             MOVE 'VWMCN00 '      TO ABT-DA-ACCESS-NAME
007870             MOVE '2110-   '      TO ABT-ERROR-SECTION
007880             PERFORM Z-980-ABNORMAL-TERM
007890     END-EVALUATE.
007900
007910 2110-EXIT.  EXIT.
007920******************************************************************
007930*  THIS ROUTINE CHECK BRANCH, REGION, AREA AND COMPANY
007940******************************************************************
007950 2200-GET-XEROX.
007960
007970     MOVE MXWW26-CNTL-ENT-NO TO CNTL-ENT-NO OF DCLVWMCN00.
007980     EXEC SQL
007990         SELECT COMPANY_NO,
008000                AREA_NO,
008010                REGION_NO,
008020                LANGUAGE_IND,
008030                CNTL_ENT_TYPE_CODE
008040         INTO :DCLVWMCN00.COMPANY-NO,
008050              :DCLVWMCN00.AREA-NO,
008060              :DCLVWMCN00.REGION-NO,
008070              :DCLVWMCN00.LANGUAGE-CODE,
008080              :DCLVWMCN00.CNTL-ENT-TYPE-CODE
008090         FROM VWMCN00
008100         WHERE CNTL_ENT_NO = :DCLVWMCN00.CNTL-ENT-NO
008110     END-EXEC.
008120
008130     IF SQLCODE = 0
008140        CONTINUE
008150     ELSE
008160        DISPLAY 'CONTROL ENTITY NOT FOUND ON CN00'
008170        DISPLAY 'CNTL-ENT-NO:  '  MXWW26-CNTL-ENT-NO
008180        DISPLAY 'LANGUAGE-CODE: ' LANGUAGE-CODE OF DCLVWMCN00
008190        SET ABT-ERROR-IS-DB2 TO TRUE
008200        SET ABT-DO-ABEND     TO TRUE
008210        MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
008220        MOVE 'SELECT  '      TO ABT-DA-FUNCTION
008230        MOVE 'VWMCN00 '      TO ABT-DA-ACCESS-NAME
008240        MOVE '2300-   '      TO ABT-ERROR-SECTION
008250        PERFORM Z-980-ABNORMAL-TERM
008260     END-IF.
008270
008280     MOVE MXWW26-CNTL-ENT-NO            TO WS-CNTL-ENT-NO.
008290     PERFORM 2300-GET-XEROX-FORM-DATA THRU 2300-EXIT.
008300     IF WS-XEROX-CODE-FOUND
008310        GO TO 2200-EXIT
008320     END-IF.
008330
008340     IF REGION-NO OF DCLVWMCN00 > ZERO
008350         MOVE REGION-NO OF DCLVWMCN00 TO WS-CNTL-ENT-NO
008360         PERFORM 2300-GET-XEROX-FORM-DATA THRU 2300-EXIT
008370         IF WS-XEROX-CODE-FOUND
008380            GO TO 2200-EXIT
008390         END-IF
008400     END-IF.
008410
008420     IF AREA-NO OF DCLVWMCN00 > ZERO
008430         MOVE AREA-NO   OF DCLVWMCN00 TO WS-CNTL-ENT-NO
008440         PERFORM 2300-GET-XEROX-FORM-DATA THRU 2300-EXIT
008450         IF WS-XEROX-CODE-FOUND
008460            GO TO 2200-EXIT
008470         END-IF
008480     END-IF.
008490
008500     IF COMPANY-NO OF DCLVWMCN00 > ZERO
008510         MOVE COMPANY-NO OF DCLVWMCN00 TO WS-CNTL-ENT-NO
008520         PERFORM 2300-GET-XEROX-FORM-DATA THRU 2300-EXIT
008530         IF WS-XEROX-CODE-FOUND
008540            GO TO 2200-EXIT
008550         END-IF
008560     END-IF.
008570
008580     MOVE 1                        TO WS-CNTL-ENT-NO
008590     PERFORM 2300-GET-XEROX-FORM-DATA THRU 2300-EXIT.
008600     IF WS-XEROX-CODE-FOUND
008610         GO TO 2200-EXIT
008620     ELSE
008630         MOVE SPACES               TO MXWW26-XEROX-FORM-ID
008640                                      MXWW26-XEROX-LOGO-ID
008650         MOVE ZEROES               TO MXWW26-FORM-COPY-CNT
008660     END-IF.
008670
008680 2200-EXIT.
008690     EXIT.
008700
008710 2300-GET-XEROX-FORM-DATA.
008720******************************************************************
008730*  THIS ROUTINE WILL RETURN THE XEROX FORM ID, XEROX LOGO ID AND
008740*  NUMBER OF COPIES OF THE REPORT TO BE PRINTED TO THE CALLED PGM
008750******************************************************************
008760
008770     MOVE WS-CNTL-ENT-NO     TO WS-SXK-CNTL-ENT-NO.
008780     MOVE MXWW26-LANG-CODE   TO WS-SXK-LANG-CODE.
008790     MOVE MXWW26-CNTRY-CODE  TO WS-SXK-CNTRY-CODE.
008800
008810     PERFORM 2350-SEARCH-XEROX-FORM-TABLE THRU 2350-EXIT.
008820     IF WS-XEROX-CODE-FOUND
008830         GO TO 2300-EXIT
008840     END-IF.
008850
008860     MOVE WS-CNTL-ENT-NO TO WS-SXK-CNTL-ENT-NO.
008870     MOVE SPACES             TO WS-SXK-LANG-CODE.
008880     MOVE MXWW26-CNTRY-CODE  TO WS-SXK-CNTRY-CODE.
008890
008900     PERFORM 2350-SEARCH-XEROX-FORM-TABLE THRU 2350-EXIT.
008910     IF WS-XEROX-CODE-FOUND
008920         GO TO 2300-EXIT
008930     END-IF.
008940
008950     MOVE WS-CNTL-ENT-NO TO WS-SXK-CNTL-ENT-NO.
008960     MOVE MXWW26-LANG-CODE   TO WS-SXK-LANG-CODE.
008970     MOVE SPACES             TO WS-SXK-CNTRY-CODE.
008980
008990     PERFORM 2350-SEARCH-XEROX-FORM-TABLE THRU 2350-EXIT.
009000     IF WS-XEROX-CODE-FOUND
009010         GO TO 2300-EXIT
009020     END-IF.
009030
009040     MOVE WS-CNTL-ENT-NO TO WS-SXK-CNTL-ENT-NO.
009050     MOVE SPACES             TO WS-SXK-LANG-CODE.
009060     MOVE SPACES             TO WS-SXK-CNTRY-CODE.
009070
009080     PERFORM 2350-SEARCH-XEROX-FORM-TABLE THRU 2350-EXIT.
009090     IF WS-XEROX-CODE-FOUND
009100         GO TO 2300-EXIT
009110     END-IF.
009120
009130 2300-EXIT.
009140     EXIT.
009150
009160 2350-SEARCH-XEROX-FORM-TABLE.
009170******************************************************************
009180*  THIS ROUTINE SEARCHES THE XEROX FORM CODE TABLE.              *
009190******************************************************************
009200
009210     SET WS-SEARCH-FOR-FORM           TO TRUE.
009220
009230     PERFORM
009240     VARYING WS-SUB
009250     FROM    1 BY 1
009260     UNTIL   WS-XEROX-CODE-FOUND
009270     OR      WS-XEROX-CODE-NOT-FOUND
009280         SET XERX-SUB1                TO WS-SUB
009290         EVALUATE TRUE
      ** FGTN-2146-S
009300*        WHEN WS-SUB > 100
               WHEN WS-SUB > 500
      ** FGTN-2146-E
009310             SET WS-XEROX-CODE-NOT-FOUND TO TRUE
009320         WHEN WS-XEROX-DATA-KEY     (XERX-SUB, XERX-SUB1)
009330            = SPACES
009340             SET WS-XEROX-CODE-NOT-FOUND TO TRUE
009350         WHEN WS-XEROX-DATA-KEY     (XERX-SUB, XERX-SUB1)
009360            = WS-SEARCH-XEROX-DATA-KEY
009370             SET WS-XEROX-CODE-FOUND     TO TRUE
009380             MOVE WS-XEROX-FORM-ID  (XERX-SUB, XERX-SUB1)
009390               TO MXWW26-XEROX-FORM-ID
009400             MOVE WS-XEROX-LOGO-ID  (XERX-SUB, XERX-SUB1)
009410               TO MXWW26-XEROX-LOGO-ID
009420             MOVE WS-XEROX-COPY-CNT (XERX-SUB, XERX-SUB1)
009430               TO MXWW26-FORM-COPY-CNT
009440         END-EVALUATE
009450     END-PERFORM.
009460
009470 2350-EXIT.  EXIT.
009480/*****************************************************************
009490*  THE PROGRAM IS ATTEMPTING TO ESTABLISH WHO TO MAIL THE XEROX  *
009500*  REPORT TO                                                     *
009510******************************************************************
009520 2400-RTN-SPEC-HNDL.
009530
009531     EVALUATE CNTL-ENT-TYPE-CODE OF DCLVWMCN00
009532     WHEN 'C'
009550        MOVE ZERO                      TO WS-BRANCH
009551        MOVE CNTL-ENT-NO OF DCLVWMCN00 TO WS-COMPANY
009560     WHEN 'B'
009561        MOVE CNTL-ENT-NO OF DCLVWMCN00 TO WS-BRANCH
009570        MOVE COMPANY-NO  OF DCLVWMCN00 TO WS-COMPANY
009571     WHEN OTHER
009572        MOVE ZERO                      TO WS-BRANCH
009573        MOVE COMPANY-NO  OF DCLVWMCN00 TO WS-COMPANY
009580     END-EVALUATE.
009590
009600     PERFORM 2500-FIND-PGM-SPEC-HNDL THRU 2500-EXIT.
009610     IF WS-PGM-SPEC-FOUND
009620        IF WS-PGM-SPEC-HNDL-FLAG (PGM-SUB1) = 'PR'
009630           PERFORM 2700-FIND-CMPY-SPEC THRU 2700-EXIT
009640           MOVE WS-CMPY-SPEC-HNDL-FLAG (CMPY-SUB1)
009650                                 TO MXWW26-SPECIAL-HANDLING
009660        ELSE
009670           IF WS-PGM-SPEC-HNDL-FLAG (PGM-SUB1) = 'PM'
009680              PERFORM 2700-FIND-CMPY-SPEC THRU 2700-EXIT
009690              IF WS-CMPY-SPEC-HNDL-FLAG (CMPY-SUB1)(1:1) = 'H'
009700                 MOVE 'H '          TO MXWW26-SPECIAL-HANDLING
009710              ELSE
009720                 MOVE '  '          TO MXWW26-SPECIAL-HANDLING
009730              END-IF
009740           ELSE
009750              MOVE WS-PGM-SPEC-HNDL-FLAG (PGM-SUB1)
009760                                 TO MXWW26-SPECIAL-HANDLING
009770           END-IF
009780        END-IF
009790     ELSE
009800        PERFORM 2600-FIND-SPEC-HNDL THRU 2600-EXIT
009810        IF WS-SPEC-FOUND
009820           IF WS-SPEC-HNDL-FLAG (SUB1) = 'PR'
009830              PERFORM 2700-FIND-CMPY-SPEC THRU 2700-EXIT
009840              MOVE WS-CMPY-SPEC-HNDL-FLAG (CMPY-SUB1)
009850                                    TO MXWW26-SPECIAL-HANDLING
009860           ELSE
009870              IF WS-SPEC-HNDL-FLAG (SUB1) = 'PM'
009880                 PERFORM 2700-FIND-CMPY-SPEC THRU 2700-EXIT
009890                 IF WS-CMPY-SPEC-HNDL-FLAG (CMPY-SUB1)(1:1) = 'H'
009900                    MOVE 'H '          TO MXWW26-SPECIAL-HANDLING
009910                 ELSE
009920                    MOVE '  '          TO MXWW26-SPECIAL-HANDLING
009930                 END-IF
009940              ELSE
009950                 MOVE WS-SPEC-HNDL-FLAG (SUB1)
009960                                       TO MXWW26-SPECIAL-HANDLING
009970              END-IF
009980           END-IF
009990        ELSE
010000           PERFORM 2700-FIND-CMPY-SPEC THRU 2700-EXIT
010010           IF WS-CMPY-SPEC-FOUND
010020              IF WS-CMPY-SPEC-HNDL-FLAG (CMPY-SUB1)(1:1) = 'H'
010030                 MOVE 'H '          TO MXWW26-SPECIAL-HANDLING
010040              ELSE
010050                 MOVE '  '          TO MXWW26-SPECIAL-HANDLING
010060              END-IF
010070           ELSE
010080              MOVE '  '          TO MXWW26-SPECIAL-HANDLING
010090           END-IF
010100        END-IF
010110     END-IF.
010120
010130 2400-EXIT.  EXIT.
010140/*****************************************************************
010150*  THIS PARAGRAPH WILL SET UP THE SEARCH KEY
010160******************************************************************
010170 2500-FIND-PGM-SPEC-HNDL.
010180
010190     MOVE ZERO               TO WS-SPK-COMPANY-NO
010200     MOVE SPACES             TO WS-SPK-COUNTRY-CODE
010210     MOVE MXWW26-CUST-NO     TO WS-SPK-CUST-NO
010220     MOVE MXWW26-LOC-NO      TO WS-SPK-LOC-NO
010230
010240     PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT.
010250     IF WS-PGM-SPEC-FOUND
010260         GO TO 2500-EXIT
010270     END-IF.
010280
010290     MOVE ZERO               TO WS-SPK-COMPANY-NO
010300     MOVE SPACES             TO WS-SPK-COUNTRY-CODE
010310     MOVE MXWW26-CUST-NO     TO WS-SPK-CUST-NO
010320     MOVE ZERO               TO WS-SPK-LOC-NO
010330
010340     PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT.
010350     IF WS-PGM-SPEC-FOUND
010360         GO TO 2500-EXIT
010370     END-IF.
010380
010381     IF WS-BRANCH > ZERO
010390        MOVE WS-BRANCH          TO WS-SPK-COMPANY-NO
010400        MOVE MXWW26-CNTRY-CODE  TO WS-SPK-COUNTRY-CODE
010410        MOVE ZERO               TO WS-SPK-CUST-NO
010420        MOVE ZERO               TO WS-SPK-LOC-NO
010430
010440        PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT
010450        IF WS-PGM-SPEC-FOUND
010460            GO TO 2500-EXIT
010470        END-IF
010471     END-IF.
010472
010473     MOVE WS-COMPANY         TO WS-SPK-COMPANY-NO
010474     MOVE MXWW26-CNTRY-CODE  TO WS-SPK-COUNTRY-CODE
010475     MOVE ZERO               TO WS-SPK-CUST-NO
010476     MOVE ZERO               TO WS-SPK-LOC-NO
010477
010478     PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT.
010479     IF WS-PGM-SPEC-FOUND
010480         GO TO 2500-EXIT
010481     END-IF.
010482
010483     IF WS-BRANCH > ZERO
010484        MOVE WS-BRANCH          TO WS-SPK-COMPANY-NO
010485        MOVE SPACES             TO WS-SPK-COUNTRY-CODE
010486        MOVE ZERO               TO WS-SPK-CUST-NO
010487        MOVE ZERO               TO WS-SPK-LOC-NO
010488
010489        PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT
010490        IF WS-PGM-SPEC-FOUND
010491            GO TO 2500-EXIT
010492        END-IF
010493     END-IF.
010494
010495     MOVE WS-COMPANY         TO WS-SPK-COMPANY-NO
010500     MOVE SPACES             TO WS-SPK-COUNTRY-CODE
010510     MOVE ZERO               TO WS-SPK-CUST-NO
010520     MOVE ZERO               TO WS-SPK-LOC-NO
010530
010540     PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT.
010550     IF WS-PGM-SPEC-FOUND
010560         GO TO 2500-EXIT
010570     END-IF.
010580
010590     MOVE ZERO               TO WS-SPK-COMPANY-NO
010600     MOVE MXWW26-CNTRY-CODE  TO WS-SPK-COUNTRY-CODE
010610     MOVE ZERO               TO WS-SPK-CUST-NO
010620     MOVE ZERO               TO WS-SPK-LOC-NO
010630
010640     PERFORM 2550-SEARCH-PGM-SPEC-HNDL-TBL THRU 2550-EXIT.
010650     IF WS-PGM-SPEC-FOUND
010660         GO TO 2500-EXIT
010670     END-IF.
010680
010690 2500-EXIT.
010700     EXIT.
010710
010720 2550-SEARCH-PGM-SPEC-HNDL-TBL.
010730******************************************************************
010740*  THIS ROUTINE WILL RETURN THE PROGRAM SPECIFIC SPECIAL HANDLING
010750*  FLAG
010760******************************************************************
010770
010780     SET WS-SEARCH-FOR-PGM-SPEC       TO TRUE.
010790
010800     PERFORM
010810     VARYING WS-SUB
010820     FROM    1 BY 1
010830     UNTIL   WS-PGM-SPEC-FOUND
010840     OR      WS-PGM-SPEC-NOT-FOUND
010850         SET PGM-SUB1                 TO WS-SUB
010860         EVALUATE TRUE
010870         WHEN WS-SUB > 5000
010880             SET WS-PGM-SPEC-NOT-FOUND TO TRUE
010890         WHEN WS-PGM-SPEC-HNDL-KEY      (PGM-SUB1)
010900            = SPACES
010910             SET WS-PGM-SPEC-NOT-FOUND TO TRUE
010920         WHEN WS-PGM-SPEC-HNDL-KEY      (PGM-SUB1)
010930            = WS-SEARCH-PGM-SPEC-KEY
010940             SET WS-PGM-SPEC-FOUND     TO TRUE
010950             MOVE WS-PGM-SPEC-HNDL-FLAG (PGM-SUB1)
010960               TO MXWW26-SPECIAL-HANDLING
010970         END-EVALUATE
010980     END-PERFORM.
010990
011000 2550-EXIT.
011010     EXIT.
011020
011030 2600-FIND-SPEC-HNDL.
011040******************************************************************
011050*  THIS PARAGRAPH WILL SET UP THE SEARCH KEY
011060******************************************************************
011070
011080     MOVE ZERO               TO WS-SSK-COMPANY-NO
011090     MOVE SPACES             TO WS-SSK-COUNTRY-CODE
011100     MOVE MXWW26-CUST-NO     TO WS-SSK-CUST-NO
011110     MOVE MXWW26-LOC-NO      TO WS-SSK-LOC-NO
011120
011130     PERFORM 2650-SEARCH-SPEC-HNDL-TBL THRU 2650-EXIT.
011140     IF WS-SPEC-FOUND
011150         GO TO 2600-EXIT
011160     END-IF.
011170
011180     MOVE ZERO               TO WS-SSK-COMPANY-NO
011190     MOVE SPACES             TO WS-SSK-COUNTRY-CODE
011200     MOVE MXWW26-CUST-NO     TO WS-SSK-CUST-NO
011210     MOVE ZERO               TO WS-SSK-LOC-NO
011220
011230     PERFORM 2650-SEARCH-SPEC-HNDL-TBL THRU 2650-EXIT.
011240     IF WS-SPEC-FOUND
011250         GO TO 2600-EXIT
011260     END-IF.
011261
011262     IF WS-BRANCH > ZERO
011263        MOVE WS-BRANCH          TO WS-SSK-COMPANY-NO
011264        MOVE SPACES             TO WS-SSK-COUNTRY-CODE
011265        MOVE ZERO               TO WS-SSK-CUST-NO
011266        MOVE ZERO               TO WS-SSK-LOC-NO
011267
011268        PERFORM 2650-SEARCH-SPEC-HNDL-TBL THRU 2650-EXIT
011269        IF WS-SPEC-FOUND
011270            GO TO 2600-EXIT
011271        END-IF
011272     END-IF.
011273
011280     MOVE WS-COMPANY         TO WS-SSK-COMPANY-NO
011290     MOVE MXWW26-CNTRY-CODE  TO WS-SSK-COUNTRY-CODE
011300     MOVE ZERO               TO WS-SSK-CUST-NO
011310     MOVE ZERO               TO WS-SSK-LOC-NO
011320
011330     PERFORM 2650-SEARCH-SPEC-HNDL-TBL THRU 2650-EXIT.
011340     IF WS-SPEC-FOUND
011350         GO TO 2600-EXIT
011360     END-IF.
011370
011380     MOVE ZERO               TO WS-SSK-COMPANY-NO
011390     MOVE MXWW26-CNTRY-CODE  TO WS-SSK-COUNTRY-CODE
011400     MOVE ZERO               TO WS-SSK-CUST-NO
011410     MOVE ZERO               TO WS-SSK-LOC-NO
011420
011430     PERFORM 2650-SEARCH-SPEC-HNDL-TBL THRU 2650-EXIT.
011440     IF WS-SPEC-FOUND
011450         GO TO 2600-EXIT
011460     END-IF.
011470
011480 2600-EXIT.
011490     EXIT.
011500
011510 2650-SEARCH-SPEC-HNDL-TBL.
011520******************************************************************
011530*  THIS ROUTINE WILL RETURN THE PROGRAM SPECIFIC SPECIAL HANDLING
011540*  FLAG
011550******************************************************************
011560
011570     SET WS-SEARCH-FOR-SPEC           TO TRUE.
011580
011590     PERFORM
011600     VARYING WS-SUB
011610     FROM    1 BY 1
011620     UNTIL   WS-SPEC-FOUND
011630     OR      WS-SPEC-NOT-FOUND
011640         SET SUB1                     TO WS-SUB
011650         EVALUATE TRUE
011660         WHEN WS-SUB > 500
011670             SET WS-SPEC-NOT-FOUND TO TRUE
011680         WHEN WS-SPEC-HNDL-KEY          (SUB1)
011690            = SPACES
011700             SET WS-SPEC-NOT-FOUND TO TRUE
011710         WHEN WS-SPEC-HNDL-KEY          (SUB1)
011720            = WS-SEARCH-SPEC-KEY
011730             SET WS-SPEC-FOUND         TO TRUE
011740             MOVE WS-SPEC-HNDL-FLAG (SUB1)
011750               TO MXWW26-SPECIAL-HANDLING
011760         END-EVALUATE
011770     END-PERFORM.
011780
011790 2650-EXIT.
011800     EXIT.
011810
011820 2700-FIND-CMPY-SPEC.
011830******************************************************************
011840*  THIS PARAGRAPH WILL SET UP THE SEARCH KEY
011850******************************************************************
011860
011870     MOVE WS-BRANCH          TO WS-SCK-COMPANY-NO.
011880
011890     PERFORM 2750-SEARCH-CMPY-SPEC-TBL THRU 2750-EXIT.
011900     IF WS-CMPY-SPEC-FOUND
011910         GO TO 2700-EXIT
011920     END-IF.
011921
011922     MOVE WS-COMPANY         TO WS-SCK-COMPANY-NO.
011923
011924     PERFORM 2750-SEARCH-CMPY-SPEC-TBL THRU 2750-EXIT.
011925     IF WS-CMPY-SPEC-FOUND
011926         GO TO 2700-EXIT
011927     END-IF.
011930
011940 2700-EXIT.
011950     EXIT.
011960
011970 2750-SEARCH-CMPY-SPEC-TBL.
011980******************************************************************
011990*  THIS ROUTINE WILL RETURN THE PROGRAM SPECIFIC SPECIAL HANDLING
012000*  FLAG
012010******************************************************************
012020     SET WS-SEARCH-FOR-CMPY-SPEC      TO TRUE.
012030
012040     PERFORM
012050     VARYING WS-SUB
012060     FROM    1 BY 1
012070     UNTIL   WS-CMPY-SPEC-FOUND
012080     OR      WS-CMPY-SPEC-NOT-FOUND
012090         SET CMPY-SUB1                TO WS-SUB
012100         EVALUATE TRUE
012110         WHEN WS-SUB > 500
012120             SET WS-CMPY-SPEC-NOT-FOUND TO TRUE
012130         WHEN WS-CMPY-KEY                (CMPY-SUB1)
012140            = SPACES
012150             SET WS-CMPY-SPEC-NOT-FOUND TO TRUE
012160         WHEN WS-CMPY-KEY                (CMPY-SUB1)
012170            = WS-SEARCH-CMPY-KEY
012180             SET WS-CMPY-SPEC-FOUND         TO TRUE
012190             MOVE WS-CMPY-SPEC-HNDL-FLAG (CMPY-SUB1)
012200               TO MXWW26-SPECIAL-HANDLING
012210         END-EVALUATE
012220     MOVE WS-CMPY-KEY (CMPY-SUB1)   TO  WS-COMPANY-HOLD-FIELD
012230     END-PERFORM.
012240
012250 2750-EXIT.
012260     EXIT.
012270
012280 3000-TERMINATION.
012290
012300     IF WS-XEROX-CODE-NOT-FOUND
012310        DISPLAY 'MXBPW026 NOT FOUND = ' MXWW26-WORK-AREA
012320     END-IF.
012330
012340     EXEC SQL
012350         CLOSE VWMCNFM-01
012360     END-EXEC.
012370
012380     IF SQLCODE NOT = 0
012390         SET ABT-ERROR-IS-DB2 TO TRUE
012400         SET ABT-DO-ABEND     TO TRUE
012410         MOVE SQLCODE         TO ABT-ERROR-ABEND-CODE
012420         MOVE 'CLOSE   '      TO ABT-DA-FUNCTION
012430         MOVE 'VWMCNFM '      TO ABT-DA-ACCESS-NAME
012440         MOVE '3000-01 '      TO ABT-ERROR-SECTION
012450         PERFORM Z-980-ABNORMAL-TERM
012460     END-IF.
012470
012480
012490 3000-EXIT.
012500     EXIT.
012510
012520     EXEC SQL
012530         INCLUDE MXWP02
012540     END-EXEC.
012550